---
title: "Gnuplot Tips"
author: "J. R. Minter"
date: "Started: 2013-07-20, Last modified: 2019-12-11"
output:
  html_document:
    css: ../theme/jm-gray-vignette.css
    number_sections: yes
    toc: yes
    toc_depth: 3
---

```{r setup, include=FALSE}
knitr::opts_chunk$set(echo = TRUE)
library(knitr)
```


[Back to Index](../README.html)

These are John Minter's tips for using gnuplot. They were
compiled from several web sources. Some helpful ones:

[The Gnuplot FAQ](http://www.gnuplot.info/faq/faq.html)

[Gnuplot tricks](http://gnuplot-tricks.blogspot.com/2009/05/gnuplot-tricks-many-say-that-it-is.html)

[gnuplotting.org](http://www.gnuplotting.org/)

# Key modifications to the plot file

## Some source information.

I like what DTSA-II writes with a date modified to ISO...

```
# Generated by the NIST EPQ Library
# 2018-09-13
# By jrminter
```

## A block with terminal information and conversion scripts

```
# set terminal postscript enhanced color "Arial" 14
# set output "amC-on-Cu-20kV.ps"
# proc-gnuplot.sh amC-on-Cu-20kV
# set terminal png
# set terminal window # for Windows
# set terminal qt     # for macos
```

## A block with font  declarations and a title

```
lablFont = "Arial,12"
titlFont = "Arial,14"
keyFont  = "Arial,12"
ticFont  = "Arial,12"

set title "My title" font titlFont
```

## Set up the scales

```
unset logscale xy
set xrange [200:350.0]
set yrange [0.0:120000.0]
```

## Set up the axes

```
set xlabel "Energy [eV]" font titlFont
set ylabel "Intensity" font titlFont
set format x "%g"
set format y "%g"
set key spacing 1.5
set xtics font ticFont
set ytics font ticFont
```

## Set up the labels

```
unset label
set label 1 "C K{/Symbol a}" at first 281.8, 119000 center font lablFont
```

## Set up a plot

We can use HTML colors

```
plot "-" with lines lw 2 lc rgb "#000000" title "Cu", \
     "-" with lines lw 2 lc rgb "#543005" title " 1 nm", \
     "-" with lines lw 2 lc rgb "#8c510a" title " 5 nm", \
     "-" with lines lw 2 lc rgb "#bf812d" title "10 nm", \
     "-" with lines lw 2 lc rgb "#dfc27d" title "15 nm", \
     "-" with lines lw 3 lc rgb "#3333cc" title "20 nm", \
     "-" with lines lw 2 lc rgb "#f6e8c3" title "25 nm", \
     "-" with lines lw 2 lc rgb "#c7eae5" title "30 nm", \
     "-" with lines lw 2 lc rgb "#80cdc1" title "35 nm", \
     "-" with lines lw 2 lc rgb "#35978f" title "40 nm", \
     "-" with lines lw 2 lc rgb "#01665e" title "45 nm", \
     "-" with lines lw 2 lc rgb "#003c30" title "50 nm", \
     "-" with impulses lt -1 notitle
```

# A helpful setup for penepma

Plotting the output from `penepma` can be challenging. I typically want to
plot the    
`pe-spect-01.dat` file. Note the change of `eV` to `keV` by the syntax    
`u ($1/1000.):2:3` in the final line in the script below:

```
# Gnuplot
# Plots the characteristic line intensities of Orthoclase at 15 kV
# simulated by penepma16
#
# 2019-12-31
# By jrminter
#
# set terminal postscript enhanced color "Arial" 14
# set output "Ir-12kV.ps"
# proc-gnuplot.sh Ir-12kV
# set terminal png
# set terminal window
# set terminal aqua

lablFont = "Arial,14"
titlFont = "Arial,16"
keyFont  = "Arial,14"
ticFont  = "Arial,12"

set key font keyFont
set tics font ticFont

unset xzeroaxis
set zero 1.0e-60
set format y "%.1te%+-3T"
#set format xy "%.1te%+-3T"
#set format z "%.1te%+-3T"
#set format cb "%.1te%+-3T"

set zero 1.0e-60
set xzeroaxis
set xrange [0:4]         # Show the relevant range...
set yrange [1e-9:*]      # Show the relevant range...

set title 'PENEPMA Simulation of Orthoclase at 15 kV' font titlFont
set key top left
set xlabel "energy [keV]" font lablFont
set ylabel "PDF [1/(sr*electron)]" font lablFont
set logscale y

plot 'pe-spect-01.dat' u ($1/1000.):2:3 notitle w lines lw 2 lc rgb '#1B325F'

```

## Using the Windows gnuplot GUI

The Windows Gnuplot GUI is helpful for visualizing plots. 

The menu bar in the top left corner of the GUI has an `Options` window

![The gnuplot options controls on the top left](png/gnuplot_menu_bar.png)

When the `Options` is clicked, it displays a pull-down menu.

![The gnuplot options controls on the top left of the menu bar](png/gnuplot_options_menu.png)

Note the `Save as EMF... (Ctl+C)` with the handy short cut. This permits
one to save the image as an Extended Meta file. The ImageMagick program
`convert` will convert the .emf file to a `.ps`, `.png` or `.pdf` file
from the command line:

```
$ convert  Orthoclase-15kV.emf  Orthoclase-15kV.pdf
```

The `png` file has reasonable resolution...

![Orthoclase png file generated with imageMagick from the command line.](png/Orthoclase-15kV.png)


# Installing and Running on MacOSX


## Installing with Homebrew

1. Install using [Homebrew](https://stackoverflow.com/questions/32421713/install-gnuplot-5-0-1-on-osx)

Note: 2019-12-30 - Recent changes really mess this up so this doesn't work
anymore. Note that HomeBrew installs into `/usr/local/bin`.

```
brew cask install aquaterm; brew install gnuplot --with-aquaterm
```

This installation no longer works any nore. Boo! Hiss!

## MacPorts

I installed Mac Ports - download the bundle [here](https://github.com/macports/macports-base/releases/).

and installed:

`sudo port -v install gnuplot`


See [here](https://ports.macports.org/port/gnuplot/summary).



2. Set the terminal

```
set terminal aqua
```

## Installing with MacPorts

This installs into `/opt/local/bin` and works better...


# Hacking the .plt file

## Gnuplot palletes

Very helpful images from [phd-bachephysicdun.blogspot.com](http://phd-bachephysicdun.blogspot.com/2014/01/)

![Pallete 1](png/gp-pal-1.png)


![Pallete 2](png/gp-pal-2.png)


![Pallete 3](png/gp-pal-3.png)


![Pallete 4](png/gp-pal-4.png)


![Pallete 5](png/gp-pal-5.png)


![Pallete Pallete 6](png/gp-pal-6.png)



![Pallete 7](png/gp-pal-7.png)


## Adding a marker line

This came from [stackoverflow](https://stackoverflow.com/questions/4457046/how-do-i-draw-a-vertical-line-in-gnuplot) from user `Throwback1986`.

The syntax to draw a line (an arrow with no head...) is:

```
set arrow from x1,y1 to x2,y2 nohead lw 2 lc rgb 'red'
```

The example below is to draw a red vertical line on a `penepma` simulation of amorphous c for the line marker:

```
set arrow from 0.277,1e-11 to 0.277,1e-8 nohead lw 2 lc rgb 'red'
```





## Changing (scaling) data for a plot

This idea came from [](http://folk.uio.no/inf3330/scripting/doc/gnuplot/Kawano/datafile2-e.html)

I needed to scale data written by penepma. Note the use of 
`($1/1000.)`.

```
# Plots x-ray energy spectra generated by 'penepma.f'

unset xzeroaxis
set zero 1.0e-60
#set format xy "%.1te%+-3T"
#set format z "%.1te%+-3T"
#set format cb "%.1te%+-3T"

#unset mouse

set zero 1.0e-60
set xzeroaxis
set xrange [*:*]
set yrange [1e-11:*]

set title 'EPMA spectrum. Detector #1'
set xlabel "energy [keV]"
set ylabel "PDF [1/(eV*sr*electron)]"
set logscale y
plot 'pe-spect-01.dat' u ($1/1000.):2:3 w errorbars ls 2, \
     'pe-spect-01.dat' u ($1/1000.):2 notitle w histeps ls 1
```

I also needed to force a cropped plot with logarithmic axis to use
log notation for the labels. This command did the trick:

```
# Force Y axis to exponential scale
set format y "10^{%L}"
```

That example came from the `tics` section of the Gnuplot
[not so Frequently Asked Questions ](http://lowrank.net/gnuplot/index-e.html)
site (**a very helpful resource**). Note: the link above is to the English
section. The site is from Japan.

## Devices

### Use postscript to get both pdf and png output

The best png output comes via postscript. Make sure ImageMagick's
**convert** program is <span style="color:red">**ahead**</span> of windows **convert** program in the
path. Then use this macro to produce <span style="color:red">**both**</span> **pdf** 
<span style="color:red">**and**</span> **png** plots at the same time!

Use something like this:

```
# easy to comment out with #
# set terminal postscript enhanced color "Arial" 14
# set output "foo.ps"
# proc-gnuplot.sh foo
# set terminal png
set terminal window
# set terminal aqua
```

Where the **proc-gnuplot.sh** script (in C:/Apps/local/path) converts the file to
pdf and crops the margins by using ps2pdf (from texlive) and convert (from ImageMagick) like this:

```
ps2pdf $1.ps temp.pdf
pdfcrop --margins 4 temp.pdf $1.pdf
rm -rf temp.pdf
convert -density 150 -geometry 68.82% $1.pdf $1.png
```
Cropping the margins is especially helpful for inclusion
in $\LaTeX$ documents. This produces a 1024 pixel wide png image of the plot.


One can also this manually using Fiji's "Import PDF" and choosing a 200 dpi from the dialog. This is nice because one can copy an image of a ROI from which an EDS spectrum was collected and paste it into the PNG of the spectrum to make a self-contained figure.


# Useful DTSA Spectrum Plot hacks

```
unset logscale x
set logscale y
set xrange [0.0:10000.0]
set yrange [1.0:5700.0]
set xlabel "Energy [eV]" font "Arial,16"
set ylabel "Intensity" font "Arial,16"
set format x "%g"
set format y "%g"
set key spacing 1.5

unset label
```

Note that one can add a title and split the plot line, like


```
# Note LaTeX
set title "Put key info here...Al_2O_3" font "Arial,16"


plot "-" with lines lt 1 lw 2 lc rgb "#9ecae1" \
     title "  0 nm Al on Si 5 kV", \
     "-" with lines lt 1 lw 2 lc rgb "#6baed6" \
     title " 20 nm Al on Si 5 kV", \
     "-" with lines lt 1 lw 2 lc rgb "#4292c6" \
     title " 40 nm Al on Si 5 kV", \
     "-" with lines lt 1 lw 2 lc rgb "#2171b5" \
     title " 60 nm Al on Si 5 kV", \
     "-" with lines lt 1 lw 2 lc rgb "#08519c" \
     title " 80 nm Al on Si 5 kV", \
     "-" with lines lt 1 lw 2 lc rgb "#08306b" \
     title "100 nm Al on Si 5 kV", \
     "-" with impulses lw 2 lt -1 notitle

```

if one only has two elements in a DTSA plt, one can

```
unset label

set label "Sn lines in black, Cl lines in red" at 400,20000

plot "-" with lines lt 1 lw 2 lc rgb "#133463" title "Sn", \
     "-" with lines lt 1 lw 2 lc rgb "#3A89C9" title "SnCl_{2}", \
     "-" with impulses lw 2 lc rgb "red" lt -1 notitle, \
     "-" with impulses lw 2 lc rgb "black" lt -1 notitle
```

followed by (at the end)

```
e
# Cl
2622.4, 34888.0
# Cl
2620.8, 17653.3
# Cl
2815.6, 1604.8
# Cl
2815.6, 3289.9
e
# Sn ***Note the split here...***
619.9, 846.0
# Sn
3444.0, 23114.0
# Sn
577.9, 169.2
# Sn
3435.5, 2621.1
# Sn
3045.0, 961.5
# Sn
4067.5, 62.4
# Sn
3750.3, 3575.7
# Sn
3662.8, 10044.4
# Sn
4132.2, 1340.6
# Sn
690.5, 3384.0
# Sn
490.0, 16.9
# Sn
3971.4, 124.8
# Sn
3272.3, 365.2
# Sn
3708.3, 2191.2
# Sn
396.2, 1692.0
# Sn
732.5, 169.2
# Sn
4463.0, 37.0
# Sn
4376.1, 742.0
# Sn
404.7, 169.2
# Sn
4376.1, 44.6
# Sn
3904.9, 3395.2
# Sn
4019.6, 74.0
e

```

## A 2-up plot

I got the idea from [here](http://stackoverflow.com/questions/10774646/gnuplot-how-to-put-two-figures-side-by-side).


```
set terminal postscript color enhanced size 10,5
set output 'figure.ps'
# proc-gnuplot.sh figure

unset title

set multiplot layout 1, 2
unset logscale xy

set title "Sin"
set xlabel "x"
set ylabel "y"
plot(sin(x))

set title "Cos"
set xlabel "x"
set ylabel "y"
plot(cos(x))

unset multiplot

```



# Color

## RGB Colors

One can use these names
[Hamburg-Schneider](http://www.uni-hamburg.de/Wiss/FB/15/Sustainability/schneider/gnuplot/colors.htm)

## HTML Colors

One can define custom palettes using
[ColorBrewer](http://colorbrewer2.org/)

R makes this easy:

```{r}
library(RColorBrewer)
pal <-brewer.pal(8,"Oranges")
pal
```

## Plotting lines

use these in gnuplot like
```
1 lt rgb "#FFF5EB"
2 lt rgb "#FEE6CE"
3 lt rgb "#FDD0A2"
4 lt rgb "#FDAE6B"
5 lt rgb "#FD8D3C"
6 lt rgb "#F16913"
7 lt rgb "#D94801"
8 lt rgb "#8C2D04"

plot "-" with lines lt 1 lw 2 lc rgb "#FDD0A2" title "100 nm Cu", ...
```

or
```{r}
pal <-brewer.pal(8,"Blues")
pal
```

## Changing label colors

Try

```
set label 3 "Si (ignored)" at first 1730,90000 \
    center textcolor rgb "skyblue"
```

# Adding symbols

```
# Setting the terminal postscript with the options
set terminal postscript eps color enhanced "Helvetica" 20

# Setting the output file name
set output 'plot.eps'

#Setting up the grid and labels
set grid
set title "A simple graphic without LaTeX"
set xrange [-pi:pi]
set xlabel "Angle {/Symbol a}"
set ylabel "{/Symbol Dw}"
set ytics 0.5
set xtics ("{/Symbol p}" -pi, "{/Symbol p}/2" -pi/2, "0" 0, \
           "{/Symbol p}/2" pi/2, "{/Symbol p}" pi)

# Setting the legend
set key horizontal below height 2
set key box lt 2 lc -1 lw 3

# The plot itself (\ is to broke lines)
set size 1,1
plot sin(x)**2 w l lt 1 lc 1 lw 3 t "sin^2({/Symbol a})", \
sin(2*x)/x w l lt 2 lc 3 lw 3 t "sin(2{/Symbol a})/{/Symbol a}", \
sin(x) w l lt 3 lc 2 lw 3 t "sin{/Symbol a})"

```

# Controlling borders

The FAQ suggests:

```
set lmargin at screen 0.05
set bmargin at screen 0.05
set rmargin at screen 0.95
set tmargin at screen 0.95
```



[Back to Index](../README.html)